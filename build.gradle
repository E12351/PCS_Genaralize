buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: '1.5.9.RELEASE'
    classpath('se.transmode.gradle:gradle-docker:1.2')
  }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'project-report'
apply plugin: 'build-dashboard'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'docker'

archivesBaseName = 'protocol-converter-service'
group = 'lk.dialog.iot'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

ext {
  jacocoVersion = '0.7.8'
  checkstyleVersion = '7.3'
  pmdVersion = '5.5.2'
  guavaVersion = '19.0'
  jadiraVersion = '5.0.0.GA'
}

repositories {
  jcenter()
}

dependencies {
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-integration'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-redis'
  compile group: 'org.springframework.integration', name: 'spring-integration-mqtt'
  compile group: 'org.springframework.integration', name: 'spring-integration-amqp'
  compile 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.1.0'
  compile group: 'com.github.jmnarloch', name: 'modelmapper-spring-boot-starter', version: '1.1.0'
  compile group: 'mysql', name: 'mysql-connector-java'
  compile group: 'org.hibernate', name: 'hibernate-core'
  compile group: 'org.hibernate', name: 'hibernate-entitymanager'
  compile group: 'ro.fortsoft.pf4j', name: 'pf4j-spring', version: '0.3.2'
  compile group: 'com.jayway.jsonpath', name: 'json-path', version: '2.2.0'
  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  runtime group: 'org.hsqldb', name: 'hsqldb'
  compile group: 'de.codecentric', name: 'spring-boot-admin-starter-client', version: '1.5.0'
  compile group: 'org.springframework.integration', name: 'spring-integration-ip'
  compile group: 'org.eclipse.californium', name: 'californium-core', version: '1.0.6'
}

configurations {
    compile.exclude module: 'slf4j-log4j12'
}

defaultTasks 'clean', 'build'

springBoot {
  executable = true
  buildInfo()
}

jacoco {
  toolVersion = jacocoVersion
}

jacocoTestReport {
  reports {
    html.enabled = true
    xml.enabled = true
    csv.enabled = true
  }
}
test.finalizedBy jacocoTestReport

checkstyle {
  toolVersion = checkstyleVersion
  config = rootProject.resources.text.fromFile('etc/checkstyle/rules.xml')
}

pmd {
  toolVersion = pmdVersion
  ruleSetConfig = rootProject.resources.text.fromFile('etc/pmd/ruleset.xml')
  ignoreFailures = true
}

check.finalizedBy projectReport

projectReport.finalizedBy buildDashboard

task buildDocker(type: Docker, dependsOn: build) {
  push = false
  applicationName = jar.baseName
  dockerfile = file('src/main/docker/Dockerfile')
  doFirst {
    copy {
      from jar
      into stageDir
    }
  }
}
